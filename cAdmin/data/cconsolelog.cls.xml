<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="cAdmin.data.CconsoleLog">
<Description>
Class for work with cconsole.log</Description>
<Super>%Persistent</Super>
<TimeCreated>63243,34907.371349</TimeCreated>

<Property name="DateTime">
<Description>
Date and Time of cconsole entry</Description>
<Type>%TimeStamp</Type>
</Property>

<Property name="Pid">
<Description>
Process id</Description>
<Type>%Integer</Type>
</Property>

<Property name="Severity">
<Description>
0 (Information), 1 (Warning), 2 (Severe), 3 (Fatal)</Description>
<Type>%Integer</Type>
</Property>

<Property name="Message">
<Description>
Text of cconsole entry</Description>
<Type>%Text</Type>
<Parameter name="LANGUAGECLASS" value="%Text.English"/>
<Parameter name="MAXLEN" value="1000"/>
</Property>

<Parameter name="MaxEntries">
<Description>
Maximal number of entries. Old ones would be deleted first</Description>
<Default>10000</Default>
</Parameter>

<Index name="MessageIndex">
<Type>bitmap</Type>
<Properties>Message(KEYS)</Properties>
</Index>

<Index name="DateTimeIndex">
<Properties>DateTime</Properties>
</Index>

<Index name="ProcessIndex">
<Properties>Pid</Properties>
</Index>

<Index name="SeverityIndex">
<Properties>Severity</Properties>
</Index>

<UDLText name="T">
<Content><![CDATA[
// do ##class(%SYS.System).WriteToConsoleLog("msg","0","1")

]]></Content>
</UDLText>

<Method name="Path">
<Description>
Path to cconsole.log. 
Requires resource %Admin_Manage</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	 //new $Roles
	 //set sc = $SYSTEM.Security.AddRoles("cAdmin")
	 try {
	 	set path = ##class(%SYSTEM.Config).ModifyConsoleFile()
	 } catch {
		 /// Do not have the resource %Admin_Manage
		 set path = ##class(%File).ManagerDirectory()_"cconsole.log" 
	 }
	 quit path
]]></Implementation>
</Method>

<Method name="Parse">
<Description>
Parse cconsole.log file, converting it lines into instances of this class</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set hash=##class(%File).SHA1Hash(..Path())
	set file=##class(%File).%New(..Path())
	do file.Open("R")
	while 'file.AtEnd
	{					
			do ..AddEntry(file.ReadLine())
	}
	set ^cAdmin("Cconsole","LastHash")=hash
	do ..PurgeOverflow()
]]></Implementation>
</Method>

<Method name="Update">
<Description>
Get new records from cconsole.log</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set hash=##class(%File).SHA1Hash(..Path())
	set lasthash=$g(^cAdmin("Cconsole","LastHash"),"0")
	quit:hash=lasthash	
	set ^cAdmin("Cconsole","LastHash")=hash
	
	set lastdate = $g(^cAdmin("Cconsole","LastDate"),"1")
	set lasttime = $g(^cAdmin("Cconsole","LastTime"),"0.001")
	set file = ##class(%File).%New(..Path())
	do file.Open("R")
	while 'file.AtEnd
	{
		set line = file.ReadLine()
		set date = $p(line,"-",1)
		try {
			set dateh = $zdh(date,,,6)
		} catch ex {
			CONTINUE
		}
		
		CONTINUE:(dateh < lastdate)
		if dateh = lastdate {
			set line = $p(line,"-",2,*)
			set time = $p(line," ",1)
			set timeodbc = $REVERSE($REPLACE($REVERSE(time),":",".",,1))
			try {
				set timeh = $zth(timeodbc)
			} catch ex {
				CONTINUE
			}
					
			CONTINUE:(timeh < lasttime)
			if (timeh = lasttime) 
			{
				set lastdatetime = $zd(lastdate,3)_" "_$zt(lasttime,1)
				set message=""
				&sql(select Message into :message from cAdmin_data.CconsoleLog where DateTime = {ts :lastdatetime})
				CONTINUE:(message = $p(line," ",4,*)) //if messages are equal, skip
			}				
		}	
		do ..AddEntry(line)
		while 'file.AtEnd
		{
			do ..AddEntry(file.ReadLine())
		}
	}
	do ..PurgeOverflow()
]]></Implementation>
</Method>

<Method name="AddEntry">
<Description>
Add one new record to database</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>line:%String</FormalSpec>
<Implementation><![CDATA[
	try {
									//dd/mm/yy-hh:mm:ss:fff (pid) severity message
		set date = $p(line,"-",1)	//dd/mm/yy
		set dateh = $zdh(date,,,6)  // internal format
		set dateodbc = $zd(dateh,3)	//yyyy-mm-dd

		set line= $p(line,"-",2,*)	//hh:mm:ss:fff (pid) severity message
			
		set time = $p(line," ",1)	//hh:mm:ss:fff
		set timeodbc = $REVERSE($REPLACE($REVERSE(time),":",".",,1)) //hh:mm:ss.fff
			
		set line = $p(line," ",2,*)				//(pid) severity message
			
		set pid = $p(line," ",1)				//(pid)
		
		set entry = ..%New()
		set entry.DateTime = dateodbc_" "_timeodbc
		set entry.Pid = $ZSTRIP(pid,"*E'N")		//pid
		set entry.Severity = $p(line," ",2)		//severity
		set entry.Message = $p(line," ",3,*)	//message
		do entry.%Save()
		set ^cAdmin("Cconsole","LastDate")=dateh
		set ^cAdmin("Cconsole","LastTime")=$zth(timeodbc)
	} catch ex {
			// Not all strings contain messages
			// do nothing
	}
]]></Implementation>
</Method>

<Method name="PurgeOverflow">
<Description><![CDATA[
Delete first 20% entries if number of Entries>MaxEntries]]></Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	&sql(select top 1 count(id) into :count from cAdmin_data.CconsoleLog)
	if count>..#MaxEntries 
	{
		set delnumber = count * 0.2
		&sql(delete from cAdmin_data.CconsoleLog where id in (select top :delnumber id from cAdmin_data.CconsoleLog order by id asc))
	}
]]></Implementation>
</Method>

<Method name="GetLast">
<Description><![CDATA[
Get last <b>num</b> entries from cconsole.log database]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>num:%Integer</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set SQL = "SELECT TOP "_num_" * FROM cAdmin_data.CconsoleLog ORDER BY id desc"
	quit ##class(cAdmin.Utils).SQLToJSON(SQL)
]]></Implementation>
</Method>

<Method name="GetLastId">
<Description>
Get last id from cconsole.log database</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Integer</ReturnType>
<Implementation><![CDATA[
	&sql(select top 1 id into :lastid from cAdmin_data.CconsoleLog order by id desc)
	quit lastid
]]></Implementation>
</Method>

<Method name="GetRandom">
<Description>
Get random entry from cconsole.log database</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set lastid = ##class(cAdmin.data.CconsoleLog).GetLastId()
	set id= $Random(lastid)+1
	set SQL = "SELECT * FROM cAdmin_data.CconsoleLog WHERE id = '"_id_"'"
	quit ##class(cAdmin.Utils).SQLToJSON(SQL)
]]></Implementation>
</Method>

<Method name="FullTextSearch">
<Description><![CDATA[
Search for all cconsole.log entries, containing <b>query</b><br>
Example: 'alert','max']]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>query:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set SQL = "SELECT * FROM cAdmin_data.CconsoleLog WHERE message %CONTAINS ("_query_")"
	quit ##class(cAdmin.Utils).SQLToJSON(SQL)
]]></Implementation>
</Method>

<Method name="DateTimeSearch">
<Description>
Search for all cconsole.log entries, from startdatetime to enddatetime
Where startdatetime,enddatetime are TimeStamps - in yyyy-mm-dd hh:mm:ss.fff format.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>startdatetime:%TimeStamp,enddatetime:%TimeStamp</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	
	set SQL = 	"SELECT * FROM cAdmin_data.CconsoleLog WHERE "_
				"DateTime BETWEEN {ts '"_startdatetime_"'} AND "_
				" {ts '"_enddatetime_"'}" 
	quit ##class(cAdmin.Utils).SQLToJSON(SQL)
]]></Implementation>
</Method>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DataLocation>^cAdmin.data.CconsoleLogD</DataLocation>
<DefaultData>CconsoleLogDefaultData</DefaultData>
<IdLocation>^cAdmin.data.CconsoleLogD</IdLocation>
<IndexLocation>^cAdmin.data.CconsoleLogI</IndexLocation>
<StreamLocation>^cAdmin.data.CconsoleLogS</StreamLocation>
<ExtentSize>100000</ExtentSize>
<Data name="CconsoleLogDefaultData">
<Value name="1">
<Value>%%CLASSNAME</Value>
</Value>
<Value name="2">
<Value>DateTime</Value>
</Value>
<Value name="3">
<Value>Pid</Value>
</Value>
<Value name="4">
<Value>Severity</Value>
</Value>
<Value name="5">
<Value>Message</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>
